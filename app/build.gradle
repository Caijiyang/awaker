apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def static gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

android {

    signingConfigs {
        config {
            keyAlias rootProject.keyAlias
            keyPassword rootProject.keyPassword
            storeFile file('keystore/awaker.jks')
            storePassword rootProject.storePassword
        }
    }

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.future.awaker"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode gitVersionCode()
        versionName "0.9.1"

        manifestPlaceholders = [
                fabric_apikey: rootProject.fabric_apikey
        ]

        multiDexEnabled true
    }

    buildTypes {

        debug {
            versionNameSuffix 'd'

        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = output.outputFile.name
            outputFileName = fileName.replace("app", "awaker-${variant.versionName}-${variant.versionCode}")
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // ViewModel and LiveData
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    // Room
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    // core ui
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'

    implementation 'com.pnikosis:materialish-progress:1.1'
    implementation 'com.just.agentweb:agentweb:1.2.6'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    implementation 'com.sun.mail:android-mail:1.5.5'
    implementation 'com.sun.mail:android-activation:1.5.5'

    implementation 'com.github.ruzhan123.Lion:movie:0.1.8'
}
